plugins {
    alias(libs.plugins.kotlin.jvm) apply false
    alias(libs.plugins.kotlin.serialization) apply false
    alias(libs.plugins.gitVersion)
}

version = computeVersion()
description = "Components to run MPS in the cloud with Kubernetes"

fun computeVersion(): Any {
    val versionFile = file("workspaces-version.txt")
    return if (versionFile.exists()) {
        versionFile.readText().trim()
    } else {
        val gitVersion: groovy.lang.Closure<String> by extra
        val version = gitVersion()
        versionFile.writeText(version)
    }
}

subprojects {
    repositories {
        mavenLocal()
        maven { url = uri("https://artifacts.itemis.cloud/repository/maven-mps/") }
        mavenCentral()
    }

    // Make builds faster by generating identical JAR files.
    // > By default, JAR files generated by Gradle (with or without Shadow)
    // > for a single project with the same source code may not be identical to each other.
    // > Sometimes it's desirable to configure a project
    // > to consistently output a byte-for-byte identical JAR on every build.
    // > Gradle supports this with the following configuration, and Shadow will correctly respect these settings too:
    // See https://imperceptiblethoughts.com/shadow/configuration/reproducible-builds/
    tasks.withType<AbstractArchiveTask> {
        isPreserveFileTimestamps = false
        isReproducibleFileOrder = true
    }
}

val mpsMajorVersions = providers.gradleProperty("mpsMajorVersions").get().split(",")

mpsMajorVersions.forEach { mpsMajorVersion ->
    val targetDir = layout.buildDirectory.dir("mps$mpsMajorVersion")

    val mps = configurations.create("mps$mpsMajorVersion")
    val mpsVersion = providers.gradleProperty("mpsVersion$mpsMajorVersion").get()

    dependencies {
        mps("com.jetbrains:mps:$mpsVersion")
    }

    tasks.register("resolveMps$mpsMajorVersion", Sync::class) {
        from(mps.resolve().map { zipTree(it) })
        into(targetDir)
    }
}